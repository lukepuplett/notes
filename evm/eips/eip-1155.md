# ERC-1155 - Multi Token Standard

https://eips.ethereum.org/EIPS/eip-1155

Requires EIP-165

 - Standard interface for contracts managing fungible or non-fungible token types.

 - Points out that ERC-721 NFTs have a single contract per token ID, supply and metadata, whereas ERC-1155 let's a single contract control many tokens each with their own metadata.

 - The functions of ERC-1155 take an `_id` parameter to distinguish the token.

 - Motivation cites games having to create thousands of token types, each with their own contract and code, resources, deployment.

 - Ability to transfer many token types at once, saving gas.

 - Nothing so far implies that ERC-1155 contracts need to implement ERC-20 or ERC-721, optionally or not.

 ```solidity
// Note: The ERC-165 identifier for this interface is 0xd9b67a26
interface ERC1155 /* is ERC165 */ {

    event TransferSingle(address indexed _operator, address indexed _from, address indexed _to, uint256 _id, uint256 _value);
    event TransferBatch(address indexed _operator, address indexed _from, address indexed _to, uint256[] _ids, uint256[] _values);
    event ApprovalForAll(address indexed _owner, address indexed _operator, bool _approved);
    event URI(string _value, uint256 indexed _id);

    function safeTransferFrom(address _from, address _to, uint256 _id, uint256 _value, bytes calldata _data) external;

    function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external;

    function balanceOf(address _owner, uint256 _id) external view returns (uint256);

    function balanceOfBatch(address[] calldata _owners, uint256[] calldata _ids) external view returns (uint256[] memory);

    function setApprovalForAll(address _operator, bool _approved) external;

    function isApprovedForAll(address _owner, address _operator) external view returns (bool);
}
```

 - The follow interface MUST be implemented by contracts wishing to be able to receive and hold tokens, along with ERC-165 interface enquiry.

```solidity
// Note: The ERC-165 identifier for this interface is 0x4e2312e0
interface ERC1155TokenReceiver {

    function onERC1155Received(address _operator, address _from, uint256 _id, uint256 _value, bytes calldata _data) external returns(bytes4);

    function onERC1155BatchReceived(address _operator, address _from, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external returns(bytes4);       
}

 ```

## Safe Transfer Rules

 - Explicit rules for how `safeTransferFrom` and `safeBatchTransferFrom` operate with respect to the `ERC1155TokenReceiver` hook functions.

 - MUST not call "on receipt" hooks for non-contract recipients, only contracts.

 - MUST not call "on receipt" hooks for mints or transfers.

 - MUST revert if the receiver is not a receiver, i.e. does not implement `ERC1155TokenReceiver` UNLESS the tokens being sent are part of a hybrid implementation of another standard, then you MAY follow that standard's rules.

 - MUST revert if the receiver is a receiver but "returns an unknown value" ?! or throws and error.

 - Recipient is a receiver and "is the recipient of one and only one balance change" e.g. `safeTransferFrom` called:

   - MUST update balances before calling receiver hook.

   - MUST have emitted the transfer event before calling receiver hook.

   - MUST call one of `onERC1155Received` or `onERC1155BatchReceived` hooks and follow its rules.

 - Recipient is a receiver and "is the recipient of more than one balance change" e.g. `safeBatchTransferFrom`.

   - MUST update all balances before calling the hook.

   - MUST emit all transfer events before calling the hook.

   - MUST call recipient's `onERC1155Received` or `onERC1155BatchReceived` as many times as necessary to account for all balance changes
   
     - and the returned hook magic value MUST be checked and acted upon as per their rules.

   - SHOULD call the batch received hook and its rules MUST be followed.

   - MAY call the single received hook and its rules MUST be followed.

 - Creators of receiver contracts which forward received tokens in their hooks:

   - Forwarding should be considered acceptance, then `safeTransferFrom` or `safeBatchTransferFrom` is called in a new "context".

     - The acceptance magic value MUST be returned after forwarding is successful.
   
   - MAY re-purpose the `_data` argument for the new context.

   - MAY revert on failed forward; MAY keep retain ownership of the token(s) on failure.

 - When NOT using `safeTransferFrom` or `safeBatchTransferFrom` for a transfer, i.e. a non-standard API used, then:

   - Balance updates and events rules above still apply.

   - Hooks still need to be called and return values checked as above, however, while the `safeTransferFrom` or `safeBatchTransferFrom` must revert if the recipient is not an implemented receiver, a non-standard function MAY continue, see "Implementation-specific transfer API rules"

## Rules

 - `safeTransferFrom` rules:

   - MUST have pre-approval for the caller to manage the tokens being transferred out of the `_from` account, see Approval section.

   - MUST revert if `_to` is zero address.

   - MUST revert if balance of token `_id` holder is less than `_value`.

   - MUST revert on any other error.

   - MUST emit the `TransferSingle` event on balance change, see `TransferSingle` and `TransferBatch` rules.

   - MUST check if `_to` is a contract, and MUST call its hook with `_data` passed intact.

 - `safeBatchTransferFrom` rules:

   - MUST have pre-approval as above.

   - MUST revert on zero address as above.

   - MUST revert if `_ids.length != _values.length`.

   - MUST revert on any insufficient balances.

   - MUST revert on any error.

   - MUST emit `TransferSingle` or `TransferBatch`, see rules below.

   - MUST update balances and emit events in the order submitted in the array.

   - MUST check if `_to` is a contract and MUST call either hook with `_data` passed intact, see rules.

 - `TransferSingle` and `TransferBatch` event rules:

   - SHOULD emit `TransferSingle` for single transfers.

     - MAY emit it multiple times but `TransferBatch` is designed for gas efficiency.

     - MUST set `_operator` to a pre-approved address, SHOULD be `msg.sender`.

     - MUST set `_from`, `_to`, `_value` and `_id` (token type) to their obvious addresses, stupid to state this.

     - MUST set `_from` to `0x0` for mints, see rules.

     - MUST set `_to` to `0x0` for burning.

   - SHOULD emit `TransferBatch` for multiple balance transfers.

     - MAY emit it with a single element in the list for singles, though use the above.

     - Essentially there's the singular `_from` and `_to` addresses and a list of `_ids` and `_values` which correspond to each other and detail the tokens to transfer.

     - Otherwise, as above.

 - Clients and exchanges MAY use total value transferred from `0x0` minus total value sent to `0x0` on both transfer events to compute circulating supply for a token ID.

 - Implementers SHOULD emit `TransferSingle` from zero to zero, with the creator as `_operator` and `_value` `0` to announce the existence of the token.

 - Restates rule about emitting events before calling hooks.

 - `onERC1155Received` hook rules:

   - Most of it is either obvious, a restatement from what's been said already, or effectively usage documentation.

   - Recipients MAY accept an increase of its balance by returning the magic bytes below, but the transfer can still revert on other failures.
   
```
bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))
```

   - Recipients MAY reject a transfer by calling `revert`, but any non-magic response MUST also cause a `revert` by the token contract.

   - All hook invocations represent "mutually exclusive" balance changes.

   - The set of all hook invocations via single or batch describes all balance changes during the transaction, in original listed order.

   - A contract MAY not call the hook if the transfer is to itself.

 - `onERC1155BatchReceived` hook rules:

   - Nothing further to add that's not obvious or beed said.

 - `ERC1155TokenReceiver` ERC-165 rules:

   - Implementation of ERC-165 looks like this:

```solidity
  function supportsInterface(bytes4 interfaceID) external view returns (bool) {
      return  interfaceID == 0x01ffc9a7 ||    // ERC-165 support (i.e. `bytes4(keccak256('supportsInterface(bytes4)'))`).
              interfaceID == 0x4e2312e0;      // ERC-1155 `ERC1155TokenReceiver` support (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
  }
```

   - If you use your own code, it must indicate support for both ERC-165 and ERC-1155 and consume no more than 10,000 gas.

 - "Implementation-specific" transfer rules:

   - Implementation-specific means your own transfer function, not `safeTransferFrom`.

   - MUST still follow the rules for the normal methods for implemented receivers, but if the interface is not implemented then it SHOULD revert but MAY continue.

   - Example in spec gives a strange scenario in which a custom transfer is made to a contract address, but it returns a non-magic value and only then does it check if it is an implemented receiver, and if so, it MUST revert because it's certainly a rejection. Perhaps this design is more optimistic and gas efficient.

   - Notes that if the recipient is not a proper receiver, it can continue to transfer but tokens may be trapped.

   - Only non-standard transfer functions can choose to allow this, i.e. official transfer functions are strict.

 - Minting and burning rules:

   - The birth of a new token ID can be announced as explained above.

   - A mint is a transfer and event rules MUST be followed, with `_from` zero.

   - Includes tokens given an initial balance, which MUST be determinable by enumerating event logs alone, i.e. MUST emit accurate events.

   - A burn is a mint transfer in reverse and same rules apply, except it says you don't have to actually transfer to zero (that's "implementation specific"), but the `_to` in the event must be zero.

   - Circulating supply MAY be computed by enumerating event logs, hence they MUST be emitted correctly.

   - Speculates that mint and burn are likely to be done with "implementation-specific" functions and so those rules apply.

## Example Constants for Magic Values

The document suddenly deviates to this code:

```solidity
bytes4 constant public ERC1155_ERC165 = 0xd9b67a26; // ERC-165 identifier for the main token standard.
bytes4 constant public ERC1155_ERC165_TOKENRECEIVER = 0x4e2312e0; // ERC-165 identifier for the `ERC1155TokenReceiver` support (i.e. `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)")) ^ bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
bytes4 constant public ERC1155_ACCEPTED = 0xf23a6e61; // Return value from `onERC1155Received` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155Received(address,address,uint256,uint256,bytes)"))`).
bytes4 constant public ERC1155_BATCH_ACCEPTED = 0xbc197c81; // Return value from `onERC1155BatchReceived` call if a contract accepts receipt (i.e `bytes4(keccak256("onERC1155BatchReceived(address,address,uint256[],uint256[],bytes)"))`).
```

## Metadata

 - Within the metadata URI field, the special string `{id}` MUST be replaced with the actual token ID in hex form.

 - The string for the hex ID MUST be lowercase alphanumeric with no `0x` prefix, AND leading-zero padded to 64 hex characters.

 - Example: `https://token-cdn-domain/{id}.json` -> `https://token-cdn-domain/000000000000000000000000000000000000000000000000000000000004cce0.json` for a client referring to token 314592/0x4CCE0.

*Note* - That odd token ID above: Grok AI says that the token ID is 314592 and that's 0x4CCE0 in hex, then padded.

*Note* - The ID itself seems to be arbitrarily made up or randomised by the contract author.

## Metadata Extensions

 - There's an optional `ERC1155Metadata_URI` extension interface your contract can implement.

 - MUST return `true` if `0x0e89341c` is passed to your `supportsInterface` function.

 - MUST emit the `URI` event if "the change can be expressed with an event", i.e. not programmatic.

 - MAY emit the `URI` event during a mint and an observer MAY fetch the metadata URI at mint time from the `uri` function, if present.

 - The `uri` function SHOULD be used to get values (plural?!) if not event was emitted.

 - The `uri` function MUST return the same value as the latest event for an `_id` if emitted - I think this means the latest `URI` event.

 - The `uri` function MUST NOT be used to check for the existence of a token as it can return a valid string for a non-existent token.

```solidity
interface ERC1155Metadata_URI {

    function uri(uint256 _id) external view returns (string memory);
}
```

*Note* - No `URI` event on that, it's on the main 1155 interface.

## ERC-1155 Metadata URI JSON Schema

 - Loosely based on ERC-721 Metadata JSON Schema but includes optional formatting for ID substitution, `{id}`, above.

Example: 

```json
{
	"name": "Asset Name",
	"description": "Lorem ipsum...",
	"image": "https:\/\/s3.amazonaws.com\/your-bucket\/images\/{id}.png",
	"properties": {
		"simple_property": "example value",
		"rich_property": {
			"name": "Name",
			"value": "123",
			"display_value": "123 Example Value",
			"class": "emphasis",
			"css": {
				"color": "#ffffff",
				"font-weight": "bold",
				"text-decoration": "underline"
			}
		},
		"array_property": {
			"name": "Name",
			"value": [1,2,3,4],
			"class": "emphasis"
		}
	}
}
```

 - If the JSON object contains a `localization` child object, its contents (`uri`, `default` and `locales`) MAY be used to provide localized values for fields that need it.

```json
{
    "title": "Token Metadata",
    "type": "object",
    "properties": {
        "name": {
            "type": "string",
            "description": "Identifies the asset to which this token represents",
        },
        "decimals": {
            "type": "integer",
            "description": "The number of decimal places that the token amount should display - e.g. 18, means to divide the token amount by 1000000000000000000 to get its user representation."
        },
        "description": {
            "type": "string",
            "description": "Describes the asset to which this token represents"
        },
        "image": {
            "type": "string",
            "description": "A URI pointing to a resource with mime type image/* representing the asset to which this token represents. Consider making any images at a width between 320 and 1080 pixels and aspect ratio between 1.91:1 and 4:5 inclusive."
        },
        "properties": {
            "type": "object",
            "description": "Arbitrary properties. Values may be strings, numbers, object or arrays.",
        },
        "localization": {
            "type": "object",
            "required": ["uri", "default", "locales"],
            "properties": {
                "uri": {
                    "type": "string",
                    "description": "The URI pattern to fetch localized data from. This URI should contain the substring `{locale}` which will be replaced with the appropriate locale value before sending the request."
                },
                "default": {
                    "type": "string",
                    "description": "The locale of the default data within the base JSON"
                },
                "locales": {
                    "type": "array",
                    "description": "The list of locales for which data is available. These locales should conform to those defined in the Unicode Common Locale Data Repository (http://cldr.unicode.org/)."
                }
            }
        }
    }
}
```

## Approval

 - `setApprovalForAll` lets an operator manage an entire set of tokens for an approver, married to `isApprovedForAll`.

 - ERC-1761 Scoped Approval Interface is recommended for providing approval for a subset of tokens.

 - An owner SHOULD be allowed to operate on their own tokens regardless of approval status.

*Note* - Presumably leaves open the possibility for account freezing, see also ERC-3643: T-REX - Token for Regulated EXchanges which is for ERC-20.

## Rationale

 - The `symbol` function, found in ERC-20 and ERC-721, was excluded as we do not believe it's globally useful for identifying a generic virtual item, asset, and is prone to collision. Shorthand symbols are useful only really in currency trading.

 - The `name` function was removed since the Metadata JSON becomes the definitive asset name.

 - Redeploying to a new contract address MUST emit events due to the balance changes, see Upgrades in the EIP.

 - If using upgradable proxy pattern then no need.

 - Only supports "safe style" transfers, so the receiver contracts can depend on hooks to always be called.

 - Guaranteed log tracing for state of world via offchain indexing services.

 - Mentions `setApprovalForAll` design again, and that restricting approval over a subset of token IDs and other rules MAY be done with an additional interface or external contract.

 - Standard token approval interfaces can be used, e.g. ERC-1761 Scoped Approval Interface which is compatible with this 1155.

 ## Backwards Compatiblity

 - To be compatible with ERC-721 behaviour when sending tokens to contract addresses, there's leeway in the revert logic, see Safe Transfer Rules above, when the recipient does not implement `ERC1155TokenReceiver`.

 - Notes that a pure implementation of a standard, non-hybrid, is preferred, but mostly it is the emission of events that's critical.

## Usage

 - RECOMMENDED clients and wallets sort the token IDs and their values in ascending order as some ERC-1155 implementations are more gas efficient with sorted data.

*Note* - Horizon Games - Multi-Token Standard: https://github.com/horizon-games/multi-token-standard demonstrates gas efficiency on sorted data.

 - There's a `balanceOfBatch` for getting balances of many owners and tokens in a single call.
 
 - The design is really for offchain apps to depend on enumerating events to keep balances in synch.

 - For mixing NFTs and FTs in the same contract, they suggest splitting the `uint256` of the `_id` space into e.g. 128-bits for the base token ID and the last 128 for the NFT ID.

```solidity
uint256 baseTokenFT = 54321 << 128;
uint256 baseTokenNFT = 12345 << 128;
uint128 indexNFT = 50;

balanceOf(msg.sender, baseTokenFT); // Get balance of the fungible base token ID 54321.
balanceOf(msg.sender, baseTokenNFT); // Get balance of the base token for non-fungible set ID 12345 (this MAY be used to get balance of the user for all of this token set if the implementation wishes as a convenience).
balanceOf(msg.sender, baseTokenNFT + indexNFT); // Get balance of the token at index 50 for non-fungible set 12345 (should be 1 if user owns the individual non-fungible token or 0 if they do not).
```

 - Another simpler way to represent NFTs is to allow a max value of 1 for each, mirroring the real world where unique items have quantity 1.

 - I don't understand the above. In principle that's obvious but I don't get it in terms of contract code.

## References

 - Lot's of references in the EIP.

 - Reference implementation here: https://github.com/enjin/erc-1155

 ## Sample

 This is the example from the GitHub repo above, with all its comments removed.

 ```solidity
 pragma solidity ^0.5.0;

import "./SafeMath.sol";
import "./Address.sol";
import "./Common.sol";
import "./IERC1155TokenReceiver.sol";
import "./IERC1155.sol";

contract ERC1155 is IERC1155, ERC165, CommonConstants
{
    using SafeMath for uint256;
    using Address for address;

    mapping (uint256 => mapping(address => uint256)) internal balances;
    mapping (address => mapping(address => bool)) internal operatorApproval;

    bytes4 constant private INTERFACE_SIGNATURE_ERC165 = 0x01ffc9a7;
    bytes4 constant private INTERFACE_SIGNATURE_ERC1155 = 0xd9b67a26;

    function supportsInterface(bytes4 _interfaceId) public view returns (bool) {
         if (_interfaceId == INTERFACE_SIGNATURE_ERC165 ||
             _interfaceId == INTERFACE_SIGNATURE_ERC1155) {
            return true;
         }

         return false;
    }

    function safeTransferFrom(address _from, address _to, uint256 _id, uint256 _value, bytes calldata _data) external {
        require(_to != address(0x0), "_to must be non-zero.");
        require(_from == msg.sender || operatorApproval[_from][msg.sender] == true, "Need operator approval for 3rd party transfers.");

        balances[_id][_from] = balances[_id][_from].sub(_value);
        balances[_id][_to]   = _value.add(balances[_id][_to]);

        emit TransferSingle(msg.sender, _from, _to, _id, _value);

        if (_to.isContract()) {
            _doSafeTransferAcceptanceCheck(msg.sender, _from, _to, _id, _value, _data);
        }
    }

    function safeBatchTransferFrom(address _from, address _to, uint256[] calldata _ids, uint256[] calldata _values, bytes calldata _data) external {
        require(_to != address(0x0), "destination address must be non-zero.");
        require(_ids.length == _values.length, "_ids and _values array length must match.");
        require(_from == msg.sender || operatorApproval[_from][msg.sender] == true, "Need operator approval for 3rd party transfers.");

        for (uint256 i = 0; i < _ids.length; ++i) {
            uint256 id = _ids[i];
            uint256 value = _values[i];

            balances[id][_from] = balances[id][_from].sub(value);
            balances[id][_to]   = value.add(balances[id][_to]);
        }

        emit TransferBatch(msg.sender, _from, _to, _ids, _values);

        if (_to.isContract()) {
            _doSafeBatchTransferAcceptanceCheck(msg.sender, _from, _to, _ids, _values, _data);
        }
    }

    function balanceOf(address _owner, uint256 _id) external view returns (uint256) {
        return balances[_id][_owner];
    }

    function balanceOfBatch(address[] calldata _owners, uint256[] calldata _ids) external view returns (uint256[] memory) {
        require(_owners.length == _ids.length);

        uint256[] memory balances_ = new uint256[](_owners.length);

        for (uint256 i = 0; i < _owners.length; ++i) {
            balances_[i] = balances[_ids[i]][_owners[i]];
        }

        return balances_;
    }

    function setApprovalForAll(address _operator, bool _approved) external {
        operatorApproval[msg.sender][_operator] = _approved;
        emit ApprovalForAll(msg.sender, _operator, _approved);
    }

    function isApprovedForAll(address _owner, address _operator) external view returns (bool) {
        return operatorApproval[_owner][_operator];
    }

    function _doSafeTransferAcceptanceCheck(address _operator, address _from, address _to, uint256 _id, uint256 _value, bytes memory _data) internal {
        require(ERC1155TokenReceiver(_to).onERC1155Received(_operator, _from, _id, _value, _data) == ERC1155_ACCEPTED, "contract returned an unknown value from onERC1155Received");
    }

    function _doSafeBatchTransferAcceptanceCheck(address _operator, address _from, address _to, uint256[] memory _ids, uint256[] memory _values, bytes memory _data) internal {
       require(ERC1155TokenReceiver(_to).onERC1155BatchReceived(_operator, _from, _ids, _values, _data) == ERC1155_BATCH_ACCEPTED, "contract returned an unknown value from onERC1155BatchReceived");
    }
}
 ```